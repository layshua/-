
上章主要介绍了 PBR 的历史和逼真的效果特征。这章将重点介绍 PBR 的核心部分的基本原理及主流的实现方案，使读者对 PBR 的核心理论有一定了解，并能掌握相关的编码。

## **3.1 PBR 基础理论和推导**

本节的理论和推导尽量简化和精简，更深入的原理和理论将在下一章阐述。

满足以下条件的光照模型才能称之为 PBR 光照模型：

*   基于微平面模型（Be based on the microfacet surface model）。
*   能量守恒（Be energy conserving）。
*   使用基于物理的 BRDF（Use a physically based BRDF）。

### **3.1.1 微平面（Microfacet）**

大多数 PBR 技术都是基于微平面理论。在此理论下，认为在微观上所有材质表面都是由很多朝向不一的微小平面组成，有的材质表面光滑一些，有的粗糙一些。

真实世界的物体表面不一定是很多微小平面组成，也可能是带有弧度或者坑坑洼洼。但对于我们肉眼能观察到的维度，PBR 的微观近似模拟方法产生的结果跟实际差别甚微。

![](1679148476503.png)

  
_所有材质表面由粗糙度不同的微小平面组成。左边材质更粗糙，右边的平滑一些。_

当光线射入这些微平面后，通常会产生镜面反射。对于越粗糙的表面，由于其朝向更无序，反射的光线更杂乱，反之，平滑的微平面，反射的光线更平齐。  

![](1679148476534.png)

  
上图左边材质表面更粗糙，反射的光线更杂乱；图右的平滑许多，反射的光线更有规律。

从微观角度来说，没有任何表面是完全光滑的。由于这些微平面已经微小到无法逐像素地继续对其进行细分，因此我们只有假设一个粗糙度 (Roughness，即 2.4.1 中提到的粗糙度) 参数，然后用统计学的方法来概略的估算微平面的粗糙程度。

我们可以基于一个平面的粗糙度来计算出某个向量的方向与微平面平均取向方向一致的概率。这个向量便是位于光线向量 $l$和视线向量 $v$之间的中间向量，被称为**半角向量 (Halfway Vector)**。  

![](1679148476577.png)

  
_半角向量 $h$是视线 $v$和入射光 $l$的中间单位向量。_

半角向量计算公式如下：

$$h = \frac{l + v}{\|l + v\|}$$

半角向量计算 GLSL 实现:

```
// lightPos是光源位置，viewPos是摄像机位置，FragPos是像素位置
vec3 lightDir   = normalize(lightPos - FragPos);
vec3 viewDir    = normalize(viewPos - FragPos);
vec3 halfwayDir = normalize(lightDir + viewDir);
```

越多的微平面取向与其半角向量一致，材质镜面反射越强越锐利。加上引入取值 0~1 的粗糙度，可以大致模拟微平面的整体取向。  

![](1679148476601.png)

  
粗糙度从 0.1~1.0 的变化图。粗糙度越小，镜面反射越亮范围越小；粗糙度越大，镜面反射越弱。

### **3.1.2 能量守恒（Energy Conservation）**

在微平面理论中，采用近似的能量守恒：出射光的总能量不超过入射光的总能量（自发光材质除外）。3.1.1 的粗糙度变化图可以看出，材质粗糙度越大，反射的范围越大，但整体亮度变暗。

那么 PBR 是如何实现近似的能量守恒呢？

为了回答这个问题，先弄清楚**镜面反射**（specular）和**漫反射**（diffuse）的区别。

一束光照到材质表面上，通常会分成**反射**（reflection）部分和**折射**（refraction）部分。反射部分直接从表面反射出去，而不进入物体内部，由此产生了镜面反射光。折射部分会进入物体内部，被吸收或者散射产生漫反射。

折射进物体内部的光如果没有被立即吸收，将会持续前进，与物体内部的微粒产生碰撞，每次碰撞有一部分能量损耗转化成热能，直至光线能量全部消耗。有些折射光线在跟微粒发生若干次碰撞之后，从物体表面射出，便会形成漫反射光。  

![](1679148476642.png)

  
_照射在平面的光被分成镜面反射和折射光，折射光在跟物体微粒发生若干次碰撞之后，有可能发射出表面，成为漫反射。_

通常情况下，PBR 会简化折射光，将平面上所有折射光都视为被完全吸收而不会散开。而有一些被称为次表面散射 (Subsurface Scattering) 技术的着色器技术会计算折射光散开后的模拟，它们可以显著提升一些材质（如皮肤、大理石或蜡质）的视觉效果，不过性能也会随着下降。

金属 (Metallic) 材质会立即吸收所有折射光，故而金属只有镜面反射，而没有折射光引起的漫反射。

回到能量守恒话题。反射光与折射光它们二者之间是互斥的，被表面反射出去的光无法再被材质吸收。故而，进入材质内部的折射光就是入射光减去反射光后余下的能量。

根据上面的能量守恒关系，可以先计算镜面反射部分，此部分等于入射光线被反射的能量所占的百分比。而折射部分可以由镜面反射部分计算得出。

```c
float kS = calculateSpecularComponent(...); // 反射/镜面部分
float kD = 1.0 - kS;                        // 折射/漫反射部分
```

通过以上代码可以看出，镜面反射部分与漫反射部分的和肯定不会超过 1.0，从而近似达到能量守恒的目的。

### **3.1.3 反射方程（Reflectance Equation）**

**渲染方程** (Render Equation) 是用来模拟光的视觉效果最好的模型。而 PBR 的渲染方程是用以抽象地描述 PBR 光照计算过程的特化版本的渲染方程，被称为**反射方程**。

PBR 的反射方程可抽象成下面的形式：

$$L_o(p,\omega_o) = \int\limits_{\Omega} f_r(p,\omega_i,\omega_o) L_i(p,\omega_i) n \cdot \omega_i d\omega_i$$

反射方程看似很复杂，但如果拆分各个部分加以解析，就可以揭开其神秘的面纱。

为了更好地理解反射方程，先了解**辐射度量学 (Radiometry)**。辐射度量学是一种用来度量电磁场辐射（包括可见光）的手段。有很多种辐射度量 (radiometric quantities) 可以用来测量曲面或者某个方向上的光，此处只讨论和反射方程有关的一种量，它就是**辐射率 (Radiance)**，用 $L$来表示。

先用一个表展示辐射度量学涉及的概念、名词、公式等信息，后面会更加详细地介绍。

|名称<img width=100>|符号|单位<img width>  |公式|解析|
|---|---|---|---|---|
|**辐射能量** <br>Radiant Energy| $Q$ |焦耳<br> $J$ |- |电磁辐射能量 |
|**辐射通量** <br>Radiant Flux| $\Phi$ |瓦<br> $W$ | $\displaystyle\Phi=\frac{dQ}{dt}$ |单位时间辐射的能量，也叫辐射功率 (Radiant Power)或通量 (Flux)|
|**辐照度**<br>Irradiance | $E$ |瓦/平方米 <br> $W/m^{2}$ | $\displaystyle\Phi=\frac{d\Phi}{dA^{\perp}}$ |**到达**单位面积的辐射通量|
|**辐射度**<br>Radiosity | $M$ |瓦/平方米<br> $W/m^2$ | $\displaystyle M=\frac{d\Phi}{dA^\perp}$ |**离开**单位面积的辐射通量，也叫辐出度、辐射出射度（Radiant Existance）|
|**辐射强度**<br>Radiant Intensity| $I$ |瓦/立体弧度<br> $W/sr$ | $\displaystyle I=\frac{d\Phi}{d\omega}$ |在单位球面上，一个光源向每**单位立体角**所投送的辐射通量|
|**辐射率**<br>Radiance| $L$ |瓦/平方米立体弧度 <br> $W/m^2sr$ | $L=\displaystyle\frac{d\Phi}{d\omega dA^{\perp}}$ |通过单位面积单位立体角的辐射通量。<br>一个辐射强度为 $\Phi$ 的光通过立体角 $\omega$ 辐射在区域 $A$ 的可被观察到的总能量。|
|**立体角**<br>Solid Angle| $\omega$ |立体弧度，球面度<br> $sr$ | $\displaystyle\omega=\frac{S}{r^{2}}$ |投射到单位球体上的一个截面的大小或者面积。是二维弧度在三维的扩展，1 球面度等于单位球体的表面面积。|

辐射率被用来量化单一方向上发射来的光线的大小或者强度。辐射率是由多个物理变量集合而成的，它涉及的物理变量有以下几种：

*   **辐射通量 (Radiant Flux)**：辐射通量用符号$\Phi$表示，表示一个光源输出的能量，以瓦特为单位。光是由多种不同波长的能量集合而成，每种波长与一种特定的（可见的）颜色相关。因此一个光源所放射出来的能量可以被视作这个光源包含的所有各种波长的一个函数。波长介于 390nm（纳米）到 700nm 的光被认为是处于可见光光谱中，也就是说它们是人眼可见的波长。
    
    ![](1679148476662.png)
    
      
    _上图展示了太阳光中不同波长的光所具有的能量。_  
    传统物理学上的辐射通量将会计算这个由不同波长构成的函数的总面积，这种计算很复杂，耗费大量性能。在 PBR 技术中，不直接使用波长的强度，而是使用三原色编码（RGB）来简化辐射通量的计算。虽然这种简化会带来一些信息上的损失，但是这对于视觉效果上的影响基本可以忽略。
    
*   **立体角 (Solid Angle)**：用符号$\omega$表示，它描述投射到单位球体上的一个截面的大小或者面积。可以把立体角想象成为一个带有体积的方向：  
    
    ![](1679148476688.png)
    
      
    更加形象地描述：观察者站在单位球面的中心，向着投影的方向看，在单位球体面上的投影轮廓的大小就是立体角。
    
*   **辐射强度 (Radiant Intensity)**：用符号 $I$表示，它描述的是在单位球面上，一个光源向每单位立体角所投送的辐射通量。举个例子，假设一个点光源向所有方向均匀地辐射能量，辐射强度就能计算出它在一个单位面积（立体角）内的能量大小：  
    
    ![](1679148476720.png)
    
      
    计算辐射强度的公式：
    

$$I = \frac{d\Phi}{d\omega}$$

其中 $I$表示辐射通量$\Phi$除以立体角$\omega$的辐射强度。

理解以上物理变量后，可以继续讨论辐射率方程了。下面方程代表的意义是：一个辐射强度为$\Phi$的光通过立体角$\omega$辐射在区域 $A$的可被观察到的总能量。

$$L=\frac{I}{dA^\perp}=\frac{\frac{d\Phi}{d\omega}}{dA\cos\theta}=\frac{d\Phi}{ dA d\omega \cos\theta}$$

笔者注：原文的公式是 $L = \frac{d^2\Phi}{ dA d\omega \cos\theta}$，经推导之后，并没有平方。​

![](1679148476742.png)

辐射率是一个区域内光照量的辐射学度量，按照光的入射（或者来源）角与平面法线的夹角$\theta$计算 $\cos \theta$。越是斜着照射在平面上光越弱，反之越是垂直照射在表面上的光越强，类似基础光照中的漫反射颜色计算，$\cos \theta$直接等于光的方向和表面法线的点积。

```
float cosTheta = dot(lightDir, N);
```

上面的物理符号似乎和 PBR 的反射方程没有直接的关系。但是，如果将立体角$\omega$跟区域 $A$都看作无限小，就可以使用辐射率来分析一束光线打在空间上一个点的通量，也就是说能够计算单束光线对单个（片元）点的辐射率影响。进一步地，将立体角$\omega$转化为方向向量$\omega$，将区域 $A$转化成点 $p$，因此在 shader 中直接使用辐射率来计算单束光线对每个片元的贡献。

实际上，当谈及光的辐射率时，通常只关注的是所有射入点 $p$的光线，这些光的辐射度总和称为**辐照度 (Irradiance)**。理解了辐射率和辐照度，回到反射方程：

$$L_o(p,\omega_o) = \int\limits_{\Omega} f_r(p,\omega_i,\omega_o) L_i(p,\omega_i) n \cdot \omega_i d\omega_i$$

渲染方程式中 $L$代表某个点 $p$的辐射率，而无限小的入射光的立体角$\omega_i$可以看作入射光方向向量$\omega_i$，将用来衡量入射光与平面法线夹角对能量的影响的 $\cos \theta$分量移出辐射率方程，作为反射方程的单独项 $n \cdot \omega_i$ 。

反射方程计算了点 $p$在所有视线方向$\omega_0$上被反射出来的辐射率 $L_o(p,\omega_o)$的总和。换言之：$L_0$计算的是在$\omega_o$方向的眼睛观察到的 $p$点的总辐照度。

反射方程里面使用的辐照度，必须要包含所有以 $p$点为中心的半球$\Omega$内的入射光，而不单单只是某一个方向的入射光。这个半球指的是围绕面法线 $n$的那一个半球：  

![](1679148476772.png)

笔者注：为什么只计算半球而不计算整个球体呢？

因为另外一边的半球因与视线方向相反，不能被观察，也就是辐射通量贡献量为 0，所以被忽略。

为了计算这个区域（半球）内的所有值，在反射方程中使用了一个称作为积分的数学符号 $\int$，来计算半球$\Omega$内所有的入射向量 $d\omega_i$。

积分计算面积的方法，有**解析 (analytically)** 和**渐近 (numerically)** 两种方法。目前尚没有可以满足渲染计算的解析法，所以只能选择离散渐近法来解决这个积分问题。

具体做法是在半球$\Omega$按一定的步长将反射方程离散地求解，然后再按照步长大小将所得到的结果平均化，这种方法被称为**黎曼和 (Riemann sum)**。下面是实现的伪代码：

```
int steps = 100; // 分段计算的数量，数量越多，计算结果越准确。
float dW  = 1.0f / steps;
vec3 P    = ...;
vec3 Wo   = ...;
vec3 N    = ...;
float sum = 0.0f;
for(int i = 0; i < steps; ++i) 
{
    vec3 Wi = getNextIncomingLightDir(i);
    sum += Fr(P, Wi, Wo) * L(P, Wi) * dot(N, Wi) * dW;
}
```

`dW`的值越小结果越接近正确的积分函数的面积或者说体积，衡量离散步长的`dW`可以看作反射方程中的 $d\omega_i$。积分计算中我们用到的 $d\omega_i$是线性连续的符号，跟代码中的`dW`并没有直接关系，但是这种方式有助于我们理解，而且这种离散渐近的计算方法总是可以得到一个很接近正确结果的值。值得一提的是，通过增加步骤数`steps`可以提高黎曼和的准确性，但计算量也会增大。

反射方程加了所有的，以各个方向$\omega_i$射入半球$\Omega$并打中点 $p$的入射光，经过反射函数 $f_r$进入观察者眼睛的所有反射光 $L_o$的辐射率之和。入射光辐射度可以由光源处获得，此外还可以利用一个环境贴图来测算所有入射方向上的辐射度。

至此，反射方程中，只剩下 $f_r$项未描述。$f_r$就是**双向反射分布函数** (Bidirectional Reflectance Distribution Function, BRDF)，它的作用是基于表面材质属性来对入射辐射度进行缩放或者加权。

### **3.1.4 双向反射分布函数（BRDF）**

**双向反射分布函数**（Bidirectional Reflectance Distribution Function，BRDF）是一个使用入射光方向$\omega_i$作为输入参数的函数，输出参数为出射光$\omega_o$，表面法线为 $n$，参数 $a$表示的是微平面的粗糙度。

BRDF 函数是近似的计算在一个给定了属性的不透明表面上每个单独的光线对最终的反射光的贡献量。假如表面是绝对光滑的（比如镜子），对于所有入射光$\omega_i$的 BRDF 函数都将会返回 0.0，除非出射光线$\omega_o$方向的角度跟入射光线$\omega_i$方向的角度以面法线为中轴线完全对称，则返回 1.0。

BRDF 对于材质的反射和折射属性的模拟基于之前讨论过的微平面理论，想要 BRDF 在物理上是合理的，就必须遵守能量守恒定律。比如反射光能量总和永远不应该超过入射光。技术上来说，Blinn-Phong 光照模型跟 BRDF 一样使用了$\omega_i$跟$\omega_o$作为输入参数，但是没有像基于物理的渲染这样严格地遵守能量守恒定律。

BRDF 有好几种模拟表面光照的算法，然而，基本上所有的**实时**渲染管线使用的都是 **Cook-Torrance BRDF**。

Cook-Torrance BRDF 分为漫反射和镜面反射两个部分：

$$f_r = k_d f_{lambert} + k_s f_{cook-torrance}$$

其中 $k_d$是入射光中被折射的比例，$k_s$是另外一部分被镜面反射的入射光。BRDF 等式左边的 $f_{lambert}$表示的是漫反射部分，这部分叫做伦勃朗漫反射（Lambertian Diffuse）。它类似于我们之前的漫反射着色，是一个恒定的算式：

$$f_{lambert} = \frac{c}{\pi}$$

其中 $c$代表的是 Albedo 或表面颜色，类似漫反射表面纹理。除以$\pi$是为了规格化漫反射光，为后期的 BRDF 积分做准备。

此处的伦勃朗漫反射跟以前用的漫反射之间的关系：以前的漫反射是用表面的漫反射颜色乘以法线与面法线的点积，这个点积依然存在，只不过是被移到了 BRDF 外面，写作 $n \cdot \omega_i$，放在反射方程 $L_o$靠后的位置。

BRDF 的高光（镜面反射）部分更复杂：

$$f_{cook-torrance} = \frac{DFG}{4(\omega_o \cdot n)(\omega_i \cdot n)}$$

Cook-Torrance 镜面反射 BRDF 由 3 个函数（$D$，$F$，$G$）和一个标准化因子构成。$D$，$F$，$G$符号各自近似模拟了特定部分的表面反射属性：

*   **$D$(Normal _D_istribution Function，NDF)**：法线分布函数，估算在受到表面粗糙度的影响下，取向方向与中间向量一致的微平面的数量。这是用来估算微平面的主要函数。
*   **$F$(_F_resnel equation)**：菲涅尔方程，描述的是在不同的表面角下表面反射的光线所占的比率。
*   **$G$(_G_eometry function)**：几何函数，描述了微平面自成阴影的属性。当一个平面相对比较粗糙的时候，平面表面上的微平面有可能挡住其他的微平面从而减少表面所反射的光线。

以上的每一种函数都是用来估算相应的物理参数的，而且你会发现用来实现相应物理机制的每种函数都有不止一种形式。它们有的非常真实，有的则性能高效。你可以按照自己的需求任意选择自己想要的函数的实现方法。

Epic Games 公司的 Brian Karis 对于这些函数的多种近似实现方式进行了大量的研究。这里将采用 Epic Games 在 Unreal Engine 4 中所使用的函数，其中 $D$使用 Trowbridge-Reitz GGX，$F$使用 Fresnel-Schlick 近似法 (Approximation)，而 $G$使用 Smith's Schlick-GGX。

#### **3.1.4.1 $D$(Normal _D_istribution Function，NDF)**

法线分布函数，从统计学上近似的表示了与某些（如中间）向量 $h$取向一致的微平面的比率。

目前有很多种 NDF 都可以从统计学上来估算微平面的总体取向度，只要给定一些粗糙度的参数以及一个我们马上将会要用到的参数 Trowbridge-Reitz GGX（GGXTR）：

$$NDF_{GGX TR}(n, h, \alpha) = \frac{\alpha^2}{\pi((n \cdot h)^2 (\alpha^2 - 1) + 1)^2}$$

这里的 $h$是用来测量微平面的半角向量，$\alpha$是表面的粗糙度，$n$是表面法线。 如果将 $h$放到表面法线和光线方向之间，并使用不同的粗糙度作为参数，可以得到下面的效果：  

![](1679148476814.png)

当粗糙度很低（表面很光滑）时，与中间向量 $h$取向一致的微平面会高度集中在一个很小的半径范围内。由于这种集中性，NDF 最终会生成一个非常明亮的斑点。但是当表面比较粗糙的时候，微平面的取向方向会更加的随机，与向量 $h$取向一致的微平面分布在一个大得多的半径范围内，但是较低的集中性也会让最终效果显得更加灰暗。

Trowbridge-Reitz GGX 的 NDF 实现代码：

```
float DistributionGGX(vec3 N, vec3 H, float a) {
    float a2     = a*a;
    float NdotH  = max(dot(N, H), 0.0);
    float NdotH2 = NdotH*NdotH;
	
    float nom    = a2;
    float denom  = (NdotH2 * (a2 - 1.0) + 1.0);
    denom        = PI * denom * denom;
	
    return nom / denom;
}
```

#### **3.1.4.2 $F$(_F_resnel equation)**

菲涅尔方程定义的是在不同观察方向上，表面上被反射的光除以被折射的光的比例。在一束光击中了表面的一瞬间，菲涅尔根据表面与观察方向之间的夹角，计算得到光被反射的百分比。根据这个比例和能量守恒定律我们可以直接知道剩余的能量就是会被折射的能量。

当我们垂直观察每个表面或者材质时都有一个基础反射率，当我们以任意一个角度观察表面时所有的反射现象都会变得更明显（反射率高于基础反射率）。你可以从你身边的任意一件物体上观察到这个现象，当你以 90 度角观察你的桌子你会法线反射现象将会变得更加的明显，理论上以完美的 90 度观察任意材质的表面都应该会出现全反射现象（所有物体、材质都有菲涅尔现象）。

菲涅尔方程同样是个复杂的方程，但是幸运的是菲涅尔方程可以使用 Fresnel-Schlick 来近似：

$$F_{Schlick}(h, v, F_0) = F_0 + (1 - F_0) ( 1 - (h \cdot v))^5$$

$F_0$表示的是表面基础反射率，这个我们可以使用一种叫做 **Indices of refraction(IOR)** 的方法计算得到。运用在球面上的效果就是你看到的那样，观察方向越是接近**掠射角**（grazing angle，又叫切线角，与正视角相差 90 度），菲涅尔现象导致的反射就越强：  

![](1679148476836.png)

菲涅尔方程中有几个微妙的地方，一个是 Fresnel-Schlick 算法仅仅是为电介质（绝缘体）表面定义的算法。对于金属表面，使用电介质的折射率来计算基础反射率是不合适的，我们需要用别的菲涅尔方程来计算。对于这个问题，我们需要预先计算表面在正视角 (即以 0 度角正视表面) 下的反应（$F_0$），然后就可以跟之前的 Fresnel-Schlick 算法一样，根据观察角度来进行插值。这样我们就可以用一个方程同时计算金属和电介质了。

表面在正视角下的反映或者说基础反射率可以在这个数据库中找到，下面是 Naty Hoffman 的在 SIGGRAPH 公开课中列举的一些常见材质的值：  

![](1679148476872.png)

这里可以观察到的一个有趣的现象，所有电介质材质表面的基础反射率都不会高于 0.17，这其实是例外而非普遍情况。导体材质表面的基础反射率起点更高一些并且（大多）在 0.5 和 1.0 之间变化。此外，对于导体或者金属表面而言基础反射率一般是带有色彩的，这也是为什么要用 RGB 三原色来表示的原因（法向入射的反射率可随波长不同而不同）。这种现象我们只能在金属表面观察的到。

金属表面这些和电介质表面相比所独有的特性引出了所谓的金属工作流的概念。也就是我们需要额外使用一个被称为金属度 (Metalness) 的参数来参与编写表面材质。金属度用来描述一个材质表面是金属还是非金属的。

通过预先计算电介质与导体的值，我们可以对两种类型的表面使用相同的 Fresnel-Schlick 近似，但是如果是金属表面的话就需要对基础反射率添加色彩。我们一般是按下面这个样子来实现的：

```
vec3 F0 = vec3(0.04);
F0      = mix(F0, surfaceColor.rgb, metalness);
```

我们为大多数电介质表面定义了一个近似的基础反射率。$F_0$取最常见的电解质表面的平均值，这又是一个近似值。不过对于大多数电介质表面而言使用 0.04 作为基础反射率已经足够好了，而且可以在不需要输入额外表面参数的情况下得到物理可信的结果。然后，基于金属表面特性，我们要么使用电介质的基础反射率要么就使用 $F_0$作来为表面颜色。因为金属表面会吸收所有折射光线而没有漫反射，所以我们可以直接使用表面颜色纹理来作为它们的基础反射率。

Fresnel Schlick 近似可以用 GLSL 代码实现：

```
vec3 fresnelSchlick(float cosTheta, vec3 F0) {
    return F0 + (1.0 - F0) * pow(1.0 - cosTheta, 5.0);
}
```

其中`cosTheta`是表面法向量 $n$与观察方向 $v$的点乘的结果。

#### **3.1.4.3 $G$(_G_eometry function)**

几何函数模拟微平面相互遮挡导致光线的能量减少或丢失的现象。  

![](1679148476912.png)

类似 NDF，几何函数也使用粗糙度作为输入参数，更粗糙意味着微平面产生自阴影的概率更高。几何函数使用由 GGX 和 Schlick-Beckmann 组合而成的模拟函数 Schlick-GGX：

$$G_{SchlickGGX}(n, v, k) = \frac{n \cdot v} {(n \cdot v)(1 - k) + k }$$

这里的 $k$是使用粗糙度$\alpha$计算而来的，用于直接光照和 IBL 光照的几何函数的参数：

$$\begin{eqnarray*} k_{direct} &=& \frac{(\alpha + 1)^2}{8} \\ k_{IBL} &=& \frac{\alpha^2}{2} \end{eqnarray*}$$

需要注意的是这里$\alpha$的值取决于你的引擎怎么将粗糙度转化成$\alpha$，在接下来的教程中我们将会进一步讨论如何和在什么地方进行这个转换。

为了有效地模拟几何体，我们需要同时考虑两个视角，视线方向（几何遮挡）跟光线方向（几何阴影），我们可以用 **Smith 函数**将两部分放到一起：

$$G(n, v, l, k) = G_{sub}(n, v, k) G_{sub}(n, l, k)$$

其中 $v$表示视线向量，$G_{sub}(n, v, k)$表示视线方向的几何遮挡；$l$表示光线向量，$G_{sub}(n, l, k)$表示光线方向的几何阴影。使用 Smith 函数与 Schlick-GGX 作为 $G_{sub}$可以得到如下所示不同粗糙度 R 的视觉效果：  

![](1679148477023.png)

几何函数是一个值域为 [0.0, 1.0] 的乘数，其中白色 (1.0) 表示没有微平面阴影，而黑色 (0.0) 则表示微平面彻底被遮蔽。

使用 GLSL 编写的几何函数代码如下：

```
float GeometrySchlickGGX(float NdotV, float k) {
    float nom   = NdotV;
    float denom = NdotV * (1.0 - k) + k;
	
    return nom / denom;
}
  
float GeometrySmith(vec3 N, vec3 V, vec3 L, float k) {
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    float ggx1 = GeometrySchlickGGX(NdotV, k); // 视线方向的几何遮挡
    float ggx2 = GeometrySchlickGGX(NdotL, k); // 光线方向的几何阴影
	
    return ggx1 * ggx2;
}
```

#### **3.1.4.4 Cook-Torrance 反射方程 (Cook-Torrance reflectance equation)**

Cook-Torrance 反射方程中的每一个部分我们我们都用基于物理的 BRDF 替换，可以得到最终的反射方程：

$$L_o(p,\omega_o) = \int\limits_{\Omega} (k_d\frac{c}{\pi} + k_s\frac{DFG}{4(\omega_o \cdot n)(\omega_i \cdot n)}) L_i(p,\omega_i) n \cdot \omega_i d\omega_i$$

上面的方程并非完全数学意义上的正确。前面提到菲涅尔项 $F$代表光在表面的反射比率，它直接影响 $k_s$因子，意味着反射方程的镜面反射部分已经隐含了因子 $k_s$。因此，最终的 Cook-Torrance 反射方程如下（去掉了 $k_s$）：

$$L_o(p,\omega_o) = \int\limits_{\Omega} (k_d\frac{c}{\pi} + \frac{DFG}{4(\omega_o \cdot n)(\omega_i \cdot n)}) L_i(p,\omega_i) n \cdot \omega_i d\omega_i$$

这个方程完整地定义了一个基于物理的渲染模型，也就是我们一般所说的基于物理的渲染（PBR）。

### **3.1.5 制作 PBR 材质**

对 PBR 数学模型有了基本了解之后，我们最后要讨论的是美工应该生成怎样的材质属性，让我们可以直接用在 PBR 渲染管线里。PBR 管线中需要的所有材质参数都可以使用纹理来定义或者模拟，使用纹理我们可以逐像素控制制定的面如何跟光线交互：这个点是否是金属，粗糙度如何又或者表面对不同波长的光有什么反映。

下面是在 PBR 渲染管线中经常用到的纹理：  

![](1679148477062.png)

下面的参数跟 [**2.4 PBR 在游戏引擎的应用**](#2-4-pbr%E5%9C%A8%E6%B8%B8%E6%88%8F%E5%BC%95%E6%93%8E%E7%9A%84%E5%BA%94%E7%94%A8)描述的很多参数基本一致。

*   **反射率**（Albedo）：反射率纹理指定了材质表面每个像素的颜色，如果材质是金属那纹理包含的就是基础反射率。这个跟我们之前用过的漫反射纹理非常的类似，但是不包含任何光照信息。漫反射纹理通常会有轻微的阴影和较暗的裂缝，这些在 Albedo 贴图里面都不应该出现，仅仅只包含材质的颜色（金属材质是基础反射率）。
    
*   **法线**（Normal）：法线纹理跟我们之前使用的是完全一样的。法线贴图可以逐像素指定表面法线，让平坦的表面也能渲染出凹凸不平的视觉效果。
    
*   **金属度**（Metallic）：金属度贴图逐像素的指定表面是金属还是电介质。根据 PBR 引擎各自的设定，金属程度即可以是 [0.0，1.0] 区间的浮点值也可以是非 0 即 1 的布尔值。
    
*   **粗糙度**（Roughness）：粗糙度贴图逐像素的指定了表面有多粗糙，粗糙度的值影响了材质表面的微平面的平均朝向，粗糙的表面上反射效果更大更模糊，光滑的表面更亮更清晰。有些 PBR 引擎用光滑度贴图替代粗糙度贴图，因为他们觉得光滑度贴图更直观，将采样出来的光滑度使用（1 - 光滑度）= 粗糙度 就能转换成粗糙度了。
    
*   **环境光遮挡**（Ambient Occlusion，AO）：AO 贴图为材质表面和几何体周边可能的位置，提供了额外的阴影效果。比如有一面砖墙，在两块砖之间的缝隙里 Albedo 贴图包含的应该是没有阴影的颜色信息，而让 AO 贴图来指定这一块需要更暗一些，这个地方光线更难照射到。AO 贴图在光照计算的最后一步使用可以显著的提高渲染效果，模型或者材质的 AO 贴图一般是在建模阶段手动生成的。
    

美术可以直接根据物体在真实世界里的物理属性，来设置和调整用于渲染的基于物理的材质。

基于物理的渲染管线最大的优势在于，材质的物理属性是不变的，无论环境光怎么样设置都能得到一个接近真实的渲染结果，这让美术的人生都变得美好了。

基于物理管线的材质可以很简单的移植到不同的渲染引擎，不管光照环境如何都能正确的渲染出一个自然的结果。
