
---
title: 06 模板测试
aliases: [06 Stencil Test]
tags: []
create_time: 2023-06-20 10:28
uid: 202306201028
banner: "![[Pasted image 20230620102842.png]]"
---

StencilTest 模板测试
# 1 理解概念
## 引例
![[Pasted image 20221030224512.png]]
-   左图为颜色缓冲区中的一张图，在模板缓冲区中我们会给这张图的每一个片元分配一个 0-255 的数字（8 位，默认为 0）
-   中、右图可以看到，我们修改了一些 0 为 1，通过自定义的一些准则，如输出模板缓冲区中 1 对应的片元的颜色；0 的不输出，最后通过模板测试的结果就如右图所示。
## 模板测试效果举例
-   ①传送门效果：可以看到左边传送门内的景象正是右侧的场景
![[Pasted image 20221030224810.png]]
-   ②Minions 讲解的一些效果，例如 3D 卡牌效果、侦探镜效果等
- -   大佬链接：[https://www.patreon.com/posts/14832618](https://www.patreon.com/posts/14832618)

![[1.gif|200]] ![[1 1.gif|200]] ![[1 2.gif|200]] ![[1 3.gif|200]]
-   ③每个正方体面显示不同场景（每个面作为蒙版来显示场景）
 《笼中窥梦》游戏场景：
 ![[y7Tcu-l8zuwKKGkVacX8qg.gif]] ![[bvS1V1NTiLs9k5UHIqnUUw.gif]]

-   对于上述的例子总结一下，这些效果基本可以归结为三层组成
-   以②中的图 4 传送门为例子，三层分别对应：门外场景、门内场景、门
-   也就是说可以理解为：包括两层物体/场景、和一层遮罩
# 2 什么是模板测试

## 从渲染管线理解

![](<images/1683366278896.png>)

在像素处理阶段的合并阶段（Merger）进行各种测试：
合并阶段是**可以配置但不可编程的**（对应图中为黄色背景）
![[Pasted image 20221030230409.jpg]]

流程：
-   **像素所有权测试→裁剪测试→透明度测试→模板测试→深度测试→透明度混合**

-   **PixelOwnershipTest（像素所有权测试）：**
-   简单来说就是控制当前屏幕像素的使用权限
-   举例：比如 unity 引擎中仅渲染 scene 和 game 窗口，即只对 scene 和 game 窗口部分的像素具有使用权限

-   **ScissorTest（裁剪测试）：**
-   在渲染窗口再定义要渲染哪一部分，默认全部渲染，可以自己控制。
-   和裁剪空间一起理解，也就是只渲染能看到的部分
-   举例：只渲染窗口的左下角部分

-   **AlphaTest（透明度测试）**
-   提前设置一个透明度阈值
-   只能实现不透明效果和全透明效果
-  举例：设置透明度 a 为 0.5，如果片元大于这个值就通过测试，如果小于 0.5 就剔除掉

-   **StencilTest（模板测试）**

-   **DepthTest（深度测试）**

-   **Blending（透明度混合）**
-   可以实现半透明效果

-   完成接下来的其他一系列操作后，我们会将合格的片元/像素输出到**帧缓冲区（FrameBuffer）**，最后渲染到屏幕上。

## 从逻辑上理解
![[Pasted image 20221030230942.png]]
-   理解：
-   **referenceValue**：当前模板缓冲片元的参考值（0~255）
-   **&readMask**：与读掩码做一个“与”操作
-   **stencilBufferValue**：模板缓冲区里的值，初始为 0
-   中间**comparisonFunction**，就是做一个比较

-   结果：
-   如果通过，这个片元就进入下一个阶段
-   未通过/抛弃，停止并且不会进入下一个阶段，也就是说不会进入颜色缓冲区

-   总结：就是**通过一定条件来判断这个片元/片元属性执行保留还是抛弃**的操作

## 从书面概念上理解

**模板缓冲区 Stencil buffer** 
-   模板缓冲区与颜色缓冲区和深度缓冲区类似，模板缓冲区可以为屏幕上的每一个像素点保存一个无符号整数值（通常为 8 位 int，0-255）。
-   这个值的意义根据程序的具体应用而定。

**模板测试**
-   渲染过程中，可以用这个值与预先设定好的参考值作（ReferenceValue）比较，根据结果来决定是否更新相应的像素点的颜色值。
-   这个比较的过程就称为**模板测试**。
-   模板测试在**透明度测试之后**，**深度测试之前**。
-   如果模板测试通过，相应的像素点更新，否则不更新。

# 3 基本原理和使用方法
## 语法表示/结构解释
![[Pasted image 20221030231544.png]]
-   **Ref**：当前片元的参考值（0-255）referenceValue
-   **ReadMask**：读掩码
-   **WriteMask**：写掩码
-   **Comp**：比较操作函数
-   **Pass**：测试通过，之后进行操作（StencilOperation，后边有详细讲解）
-   **Fail**：测试未通过，也会进行一个操作
-   **ZFail：** 模板测试通过，深度测试未通过，也可以进行一个操作

## ComparisonFunction

-   我们可以根据需求配置
![[Pasted image 20221030231708.png]]

## StencilOperation 更新值

-   有不同的更新操作，根据自己的需求进行配置
- ![[Pasted image 20221030231720.png]]

# 4 总结
-   **最重要 (用来比较的）两个值**：
-   **当前模板缓冲区值（StencilBufferValue）**、**模板参考值（ReferenceValue）**

-   模板测试主要就是对这两个值进行特定的比较操作，例如 Never、Always、Equal 等，具体参考上文的表格

-   模板测试后要对模板缓冲区的值进行更新操作，例如 Keep，Replace 等，具体参考上文表格

- 模板测试之后可以根据结果对模板缓冲区做不同的更新操作，例如模板测试成功操作 Pass、模板测试失败操作 Fail、深度测试失败操作 ZFail、还有正对正面和背面精确更新操作 Passback，Passfront，Failback 等...

属性中使用一个内置的枚举，这样就可以在外边自己选择可配置的属性了
 ![[Pasted image 20221031163235.png]]
# 5 效果实现
 ![[Pasted image 20221031163637.png]]
[(3条消息) Unity Shader: 理解Stencil buffer并将它用于一些实战案例（描边，多边形填充，反射区域限定，阴影体shadow volume阴影渲染）_liu_if_else的博客-CSDN博客](https://blog.csdn.net/liu_if_else/article/details/86316361)
## 描边
![[Pasted image 20221031201004.png|300]]
```less
Shader "Unlit/outline"
{
    Properties
    {
        _MainTex ("Texture", 2D) = "white" {}
        [HDR]_EdgeColor("EdgeColor",Color) = (1,1,1,1)
    }
    SubShader
    {
        Tags { "RenderType"="Opaque" }
        LOD 100
        
        Stencil
        {
            Ref 0
            Comp Equal
            Pass IncrSat //通过则stencilBufferValue加1
            Fail Keep    //保留当前缓冲区中的内容，即stencilBUfferValue不变
        }
        
        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            
            #include "UnityCG.cginc"

            struct appdata
            {
                float4 vertex : POSITION;
                float2 uv : TEXCOORD0;
            };

            struct v2f
            {
                float2 uv : TEXCOORD0;
                float4 pos : SV_POSITION;
            };

            sampler2D _MainTex;
            float4 _MainTex_ST;
            
            v2f vert (appdata v)
            {
                v2f o;
                o.pos = UnityObjectToClipPos(v.vertex);
                o.uv = TRANSFORM_TEX(v.uv, _MainTex);
                UNITY_TRANSFER_FOG(o,o.vertex);
                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                fixed4 col = tex2D(_MainTex, i.uv);
                return col;
            }
            ENDCG
        }
        // 第二个pass 描边
        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            
            #include "UnityCG.cginc"

            struct appdata
            {
                float4 vertex : POSITION;
                float4 normal : NORMAL;
                float2 uv : TEXCOORD0;
            };

            struct v2f
            {
                float2 uv : TEXCOORD0;
                float4 pos : SV_POSITION;
            };

            float4 _EdgeColor;
            
            v2f vert (appdata v)
            {
                v2f o;
                o.pos = UnityObjectToClipPos (v.vertex + normalize (v.normal)*0.01f);
                o.uv = v.uv;
                UNITY_TRANSFER_FOG (o,o.vertex);
                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                return _EdgeColor;
            }
            ENDCG
        }
    }
}


```
buffer 的值在当前帧结束前是不清除的，所以它可以跨越不同的 shader 与 pass。Stencil 结构写在 Subshader 中，那么下面的所有 pass 中的 stencil test 都按此运行。理想环境下，第一个 pass 渲染前屏幕上所有像素的 stencil 值都是 0，在该 pass 的 fragment shader 结束后，所有进行了渲染的像素都通过了 Ref 0 和 Comp Equal 的测试，并执行 Pass IncrSat 将 stencil 值加1。
第二个 pass 中，将顶点进行进行了放大。进行同样的 stencil 测试，上一个 pass 渲染过的像素 stencil 值已经变为 1，无法通过 Ref 0+Comp Equal 测试，那么现在只会在放大后的既是 stencil 值仍然为0的区域进行渲染。
最后对对第二个 pass 通过测试的像素给予描边颜色。

## 卡牌效果

-   **注：Unity 中模板缓冲区默认都是 0**
ID 都为 0 时，即 unity 默认的显示效果：
![[Pasted image 20221031161931.png|300]]
Mask 和 Texture 的 ID 都设置为 1：
![[Pasted image 20221031155916.png|300]]
![[Pasted image 20221031160105.png|300]]
思路：蒙版 Ref[_ID]设置为 1，Texture 的 Ref[_ID]也设置为 1，这样可以显示与蒙版重叠的部分，其余部分都剔除。

![[kapai.gif]]
**以下为案例 shader，重点在于 Stencil 部分的设置，其余部分就是按正常 shader 来写，对于 3d 模型计算一下光照**
### ①蒙版 Mask 的 shader
```less
//蒙版，ID设置为1，将外面东西全部剔除
Shader "Unlit/StencilMask"
{
    Properties
    {
        _ID ("Mask ID", Int) = 1
    }
    SubShader
    {
        Tags { "RenderType"="Opaque" "Queue" = "Geometry+1" }
        ColorMask 0 //0：什么都不显示，全透明
        //ColorMask RGBA  //四个通道全部输出，其他可选RGB、R、G、B、0
        Zwrite off
        
        Stencil
        {
            Ref[_ID]
            Comp always //默认always
            Pass replace //replace：通过就把模板缓冲区的值替换成我们的ID值，默认keep
            
            //其他不写的选项默认Keep
            //Fail Keep
            //ZFail keep
        }
        LOD 100

        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            #include "UnityCG.cginc"

            struct appdata
            {
                float4 vertex : POSITION;
            };

            struct v2f
            {
                float4 pos : SV_POSITION;
            };

            v2f vert (appdata v)
            {
                v2f o;
                o.pos = UnityObjectToClipPos(v.vertex);
                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                return float4(1,1,1,1); //随便给一个颜色，因为ColorMask为0，不会输出颜色
            }
            ENDCG
        }
    }
}

```
### ②被遮挡物体的 shader
```less
//被遮挡物体 
Shader "Unlit/TextureMasked"  
{  
    Properties  
    {  
        _MainTex("BaseColor",2D) = "white"{}  
        _ID ("Mask ID", Int) = 1  
    }  
    SubShader  
    {  
        //在Mask之后渲染，队列次序比Mask高  
        Tags { "RenderType"="Opaque" "Queue" = "Geometry+2" }  
          
        Stencil  
        {  
            Ref[_ID]  
            Comp equal  //索引值和当前模板缓冲区中的值一致时才会被绘制  
        }  
        LOD 100  
  
        Pass  
        {  
            CGPROGRAM  
            #pragma vertex vert  
            #pragma fragment frag  
  
            #include "UnityCG.cginc"  
  
            struct appdata  
            {  
                float4 vertex : POSITION;  
                float2 uv : TEXCOORD0;  
            };  
  
            struct v2f  
            {  
                float4 pos : SV_POSITION;  
                float2 uv : TEXCOORD0;  
            };  
  
            sampler2D _MainTex;  
            float4 _MainTex_ST;  
  
            v2f vert (appdata v)  
            {  
                v2f o;  
                o.uv = v.uv;  
                o.pos = UnityObjectToClipPos (v.vertex);  
                return o;  
            }  
  
            fixed4 frag (v2f i) : SV_Target  
            {  
                float4 BaseColor = tex2D(_MainTex, i.uv);  
                return BaseColor;   
            }  
            ENDCG  
        }  
    }  
    FallBack "Diffuse"  
}

```
### ③完善卡牌效果
**分离前后 Mask：**
MainTex 的 A 通道是一个 Mask，将正反面分离以定制正反面的不同效果，比如单独加 Fresnel
![[Pasted image 20221031170431.png|300]]
![[Pasted image 20221031170405.png]]
## 盒子不同面显示不同场景
 ![[bvS1V1NTiLs9k5UHIqnUUw.gif]]
 -   和卡牌效果类似，一个用蒙版遮罩的物体，盒子每个面使用一个蒙版遮罩
-   同样利用默认的值为 0 来做，只是面多了，蒙版和里边显示的物体也多了，ID 依次为 1、2、3、4
-   总结：一个蒙版对应一个物体，他们使用相同的 ID，出来的效果就是：每个面显示的盒子内部物理不同
